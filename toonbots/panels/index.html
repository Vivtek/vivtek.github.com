<html><head><title>Toon-o-matic panel-drawing module</title></head>
<body>
<h2>Toon-o-matic panel-drawing module</h2>
<hr>
So here's how we lay out and draw our panels.  As I've explained, this module is intended
to be baroque and interesting -- that may mean it will be cantankerous and hard to maintain,
but as long as it draws cartoons when I tell it to, that's good enough for me.  I'm showing
you the code just for kicks and in case you find it interesting.
<p>
In case you're wondering, this presentation is a simple form of <i>literate programming</i>,
whereby code and documentation are generated from the same source document.  This version
is rather straightforward, as I'm not inserting code blocks from other pages, just running
down the list.  If you find this easy to deal with, you might want to <a href=http://www.vivtek.com/litprog.html>read more</a>.
My open-source work is all done in a literate style.  (Helps me find problems when I need
to fix them.)
<p>
The code uses some XML functionality I wrote.  That's elsewhere on my site.
<table width=100%>
<tr><td width=30 bgcolor=eeeeee>&nbsp;</td><td width=100%>
<pre>require "_xmlapi";
</pre></td></tr></table>

The first thing we do is load our cartoon.  It's assumed to be in <code>cartoon.xml</code>
in the main cartooning directory.  This all is supposed to run in one directory, by the way,
for ease of planning.
<table width=100%>
<tr><td width=30 bgcolor=eeeeee>&nbsp;</td><td width=100%>
<pre>open O, "cartoon.xml" or die "Can't find cartoon.xml";
$cartoon = xml_read (O);
close O;
</pre></td></tr></table>

The cartoon XML notes the background image we'll be using, so let's make sure it exists.
<table width=100%>
<tr><td width=30 bgcolor=eeeeee>&nbsp;</td><td width=100%>
<pre>$background = xml_attrval ($cartoon, 'background');
die "Can't find background $background" if (!-e $background);
</pre></td></tr></table>

This phase, at least, operates by taking <code>cartoon.xml</code> and decorating it with
explicit specifications for the cartoon.  Later some of these will come from the style
library; for the time being, they're hard-coded.  Note that any of these values may be
overridden in the original <code>cartoon.xml</code>.  After this phase of processing, we'll
write the resulting XML to <code>panels.xml</code>.
<table width=100%>
<tr><td width=30 bgcolor=eeeeee>&nbsp;</td><td width=100%>
<pre>xml_set ($cartoon, 'linestyle', 'none') if !xml_attrval ($cartoon, 'linestyle');
xml_set ($cartoon, 'rowdir', 'horiz')   if !xml_attrval ($cartoon, 'rowdir');
xml_set ($cartoon, 'rowformat', '3')    if !xml_attrval ($cartoon, 'rowformat');
xml_set ($cartoon, 'border', '1')       if !xml_attrval ($cartoon, 'border');
xml_set ($cartoon, 'gutter', '7')       if !xml_attrval ($cartoon, 'gutter');
</pre></td></tr></table>

Next step is to use the Image Magick <code>identify</code> program to glean the size of
the background.  I do that in a function far below, but the results get used here.
<table width=100%>
<tr><td width=30 bgcolor=eeeeee>&nbsp;</td><td width=100%>
<pre>xml_set ($cartoon, 'panel-x', '0');
xml_set ($cartoon, 'panel-y', '0');
@size = image_geometry ($background);
xml_set ($cartoon, 'panel-w', $size[0]);
xml_set ($cartoon, 'panel-h', $size[1]);
</pre></td></tr></table>

Now, we scan the panel structure of the XML.  As we do, we also mark each and every panel
with its location and size.  The main cartoon wants to lay out its panels horizontally,
and subpanels of panels alternate default layout directions (so that unless I tell the
code otherwise, a subpanels of a top-level panel will be arranged vertically.)  I can use
the "rowformat" attribute to tell the code how many panels should go in each row.
<p></p>
There's a lot to this, and I'm not going to document each piece.  Live with it.
<table width=100%>
<tr><td width=30 bgcolor=eeeeee>&nbsp;</td><td width=100%>
<pre>@panel_list = ();

# OK, scan for panels.  Just to make the whole thing more baroque, I'm putting the recursive subroutine
# right in the middle of our script; more top-level processing goes on below this.  Isn't that cool?
panel_scan ($cartoon);
sub panel_scan {
   my $parent = shift;
   my @panels = ();

   foreach (xml_elements($parent)) {
      next if $$_{name} ne 'panel';
      push @panels, $_;
      push @panel_list, $_;
   }

   return if (!@panels);

   # Find actual row structure.
   my @rowformat = split /-/, xml_attrval ($parent, 'rowformat');
   my @actual = ($#panels + 1);
   if (xml_attrval ($parent, 'rowformat')) {
      my $rowoffset = 0;
      my $actual_offset = 0;
      $rowformat[$rowoffset] = 1 if !$rowformat[$rowoffset];
      while ($actual[$actual_offset] > $rowformat[$rowoffset]) {
         push @actual, $actual[$actual_offset] - $rowformat[$rowoffset];
         $actual[$actual_offset] = $rowformat[$rowoffset];
         $actual_offset++;
         $rowoffset++;
         $rowoffset = 0 if $rowoffset > $#rowformat;
         $rowformat[$rowoffset] = 1 if !$rowformat[$rowoffset];
      }
   }

   # Stash it for debugging and all-around baroqueness.
   xml_set ($parent, 'actual-rowformat', join ('-', @actual));

   # Now parcel out horizontal and vertical space based on the actual row structure.
   my ($row_coord, $row_width, $col_coord, $col_width);
   if (xml_attrval ($parent, 'rowdir') =~ /^v/) {
      $row_coord = 'panel-x';
      $row_width = 'panel-w';
      $col_coord = 'panel-y';
      $col_width = 'panel-h';
   } else {
      $row_coord = 'panel-y';
      $row_width = 'panel-h';
      $col_coord = 'panel-x';
      $col_width = 'panel-w';
   }

   my $rowpos = xml_attrval ($parent, $row_coord) + xml_attrval ($parent, 'border');
   my $rowtotal = xml_attrval ($parent, $row_width) - 2 * xml_attrval ($parent, 'border') - 1
                                                    - (xml_attrval ($parent, 'gutter') * (@actual - 1));
   my $rowportion = $rowtotal / @actual;
   my $row_len;
   foreach $row_len (@actual) {
      next if !$row_len;
      my $colpos = xml_attrval ($parent, $col_coord) + xml_attrval ($parent, 'border');
      my $coltotal = xml_attrval ($parent, $col_width) - 2 * xml_attrval ($parent, 'border') - 1
                                                       - (xml_attrval ($parent, 'gutter') * ($row_len - 1));
      my $colportion = $coltotal / $row_len;
      for (my $i=0; $i &lt; $row_len; $i++) { # Step along the row...
         my $panel = shift @panels;
         xml_set ($panel, $row_coord, $rowpos);
         xml_set ($panel, $col_coord, $colpos);
         xml_set ($panel, $row_width, $rowportion);
         xml_set ($panel, $col_width, $colportion);
         xml_set ($panel, 'linestyle', 'simple');

         if (!xml_attrval ($panel, 'rowdir')) {
            if (xml_attrval ($parent, 'rowdir') =~ /^v/) {
               xml_set ($panel, 'rowdir', 'horiz');
            } else {
               xml_set ($panel, 'rowdir', 'vert');
            }
         }
         xml_set ($panel, 'gutter', xml_attrval ($parent, 'gutter')) if !xml_attrval ($panel, 'gutter');

         my $panel_list_length = $#panel_list;
         panel_scan ($panel);
         if ($panel_list_length != $#panel_list) {
            # Using a side effect is baroque, isn't it?
            xml_set ($panel, 'linestyle', 'none');
         }

         $colpos += $colportion + xml_attrval ($parent, 'gutter');
      }
      $rowpos += $rowportion + xml_attrval ($parent, 'gutter');
   }
}

</pre></td></tr></table>


And now (drumroll please) we draw our panels.  Note that we're not yet taking into account
the idea that things might overlap panels.  That'll come later.  Probably much later.
The way we draw panels is pleasingly baroque; we simply build a drawing script for ImageMagick which
includes a rectangle for each panel.  Later, captions will probably be included in this script.  The
result is passed to 'convert', which is called on the background image to create the panel image.
<p></p>
So let's write out panels.txt, which is our script.  Right now, rectangular panels only (later we're
going to get much funkier, with insets and lines of varying wiggliness and stuff.)
<table width=100%>
<tr><td width=30 bgcolor=eeeeee>&nbsp;</td><td width=100%>
<pre>open P, ">panels.txt";
foreach $panel (@panel_list) {
   next if xml_attrval ($panel, 'linestyle') eq 'none';
   print P "rectangle ";
   print P xml_attrval ($panel, 'panel-x') . ',' . xml_attrval ($panel, 'panel-y');
   print P ' ';
   print P xml_attrval ($panel, 'panel-x') + xml_attrval ($panel, 'panel-w') . ',';
   print P xml_attrval ($panel, 'panel-y') + xml_attrval ($panel, 'panel-h');
   print P "\n";
}
close P;
</pre></td></tr></table>

And now we call the ImageMagick <code>convert</code> program to convert the background
image into <code>panels.gif</code>, which has a bunch of rectangles on it in the right
places.
<table width=100%>
<tr><td width=30 bgcolor=eeeeee>&nbsp;</td><td width=100%>
<pre>system "convert -stroke black -draw \@panels.txt $background panels.gif";
</pre></td></tr></table>

For the next module, we'll now write our current XML (now fully specified with respect to
panel positions and sizes) to standard output.  In my setup, this goes into <code>panels.xml</code>.
Since my setup is the only setup, that makes it pretty standard.
<table width=100%>
<tr><td width=30 bgcolor=eeeeee>&nbsp;</td><td width=100%>
<pre>xml_write (STDOUT, $cartoon);
print "\n";
</pre></td></tr></table>

Now we wrap up by defining how we call <code>identify</code> to get the size measurements
of our background image.
<table width=100%>
<tr><td width=30 bgcolor=eeeeee>&nbsp;</td><td width=100%>
<pre>sub image_geometry {
   my $background = shift;
   system "identify -verbose $background > outfile.txt"; # This is the baroque way of doing this.
   open O, "outfile.txt";
   my @s;
   while (<O>) {
      if (/Geometry:/) {
         chomp;
         @s = split /:/;
         my $size = $s[1];
         $size =~ s/ //g;
         @s = split /x/, $size;
      }
   }
   close O;
   return @s;
}
</pre></td></tr></table>

Cool, eh?  I can't wait to see what I do next.


<center>
<hr width="75%">
<table width="75%"><tr><td><font size="-1">
This code and documentation are released under the terms of the GNU license.  They are
additionally copyright (c) 2000, Vivtek.  All rights reserved except those explicitly
granted under the terms of the GNU license.  This presentation prepared using <a href=http://www.vivtek.com/lpml.html>LPML</a>.
Try literate programming.  You'll like it.
</font></td></tr></table>
</center>
</body></html>
